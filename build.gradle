plugins {
    id "groovy" 
    id "org.grails.grails-web" 
    id "org.grails.grails-gsp" 
    id "war" 
    id "idea" 
    id "com.bertramlabs.asset-pipeline"
    id "org.grails.grails-plugin" 
    id "application" 
    id "eclipse"
    id 'maven-publish'

    id "signing"
    id "io.github.gradle-nexus.publish-plugin"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

// uncomment once 6.0.1-SNAPSHOT has the following merge:
// https://github.com/grails/grails-gsp/pull/364
//def grailsSitemesh =  ':6.0.1-SNAPSHOT'
def grailsSitemesh =  ''

configurations {
    all {
        resolutionStrategy.force 'org.codehaus.groovy:groovy-xml:3.0.13'

        exclude group:'opensymphony', module:'sitemesh'
        exclude group:'org.grails', module:'grails-web-gsp-taglib'
        exclude group:'org.grails', module:'grails-web-sitemesh'
    }
}

dependencies {
// for testing purposes
//    implementation files('lib/sitemesh-3.1.0-SNAPSHOT.jar', 'lib/spring-boot-starter-sitemesh-3.1.0-SNAPSHOT-plain.jar')
    implementation('org.sitemesh:spring-boot-starter-sitemesh:3.1.0-SNAPSHOT')
    implementation('org.sitemesh:sitemesh:3.1.0-SNAPSHOT')
    implementation project(':grails-web-sitemesh2')
    implementation("org.grails.plugins:gsp:6.0.1")

    implementation("org.grails:grails-core")
    implementation("org.grails:grails-logging")
    implementation("org.grails:grails-plugin-databinding")
    implementation("org.grails:grails-plugin-i18n")
    implementation("org.grails:grails-plugin-interceptors")
    implementation("org.grails:grails-plugin-rest")
    implementation("org.grails:grails-plugin-services")
    implementation("org.grails:grails-plugin-url-mappings")
    implementation("org.grails:grails-web-boot")
    implementation("org.grails.plugins:gsp:6.0.1-SNAPSHOT")
    implementation("org.grails.plugins:hibernate5")
    implementation("org.grails.plugins:scaffolding")
    implementation("org.hibernate:hibernate-core:5.6.15.Final")
    implementation("org.springframework.boot:spring-boot-autoconfigure")
    implementation("org.springframework.boot:spring-boot-starter")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-logging")
    implementation("org.springframework.boot:spring-boot-starter-tomcat")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    compileOnly("io.micronaut:micronaut-inject-groovy")
    console("org.grails:grails-console")
    runtimeOnly("com.bertramlabs.plugins:asset-pipeline-grails:4.3.0")
    runtimeOnly("com.h2database:h2")
    runtimeOnly("javax.xml.bind:jaxb-api:2.3.1")
    runtimeOnly("org.apache.tomcat:tomcat-jdbc")
    runtimeOnly("org.fusesource.jansi:jansi:1.18")
    runtimeOnly("org.glassfish.web:el-impl:2.2.1-b05")
    testImplementation("io.micronaut:micronaut-inject-groovy")
    testImplementation("org.grails:grails-gorm-testing-support")
    testImplementation("org.grails:grails-web-testing-support")
    testImplementation("org.spockframework:spock-core")
    testImplementation("io.micronaut:micronaut-http-client")

//    implementation "javax.servlet:jstl:1.1.2"
//    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.76' // jsp example

}

application {
    mainClass.set("org.sitemesh.grails.plugins.sitemesh3.Application")
}

tasks.withType(Test) {
    useJUnitPlatform()
}
tasks.withType(War).configureEach { War war ->
    war.dependsOn compileGroovyPages
}
assets {
    minifyJs = true
    minifyCss = true
    packagePlugin = true
}

// https://github.com/grails/grails-core/issues/12213
jar {
    exclude 'gsp**'
    exclude 'META-INF/assets**'
    exclude 'messages*.properties*'
    exclude 'layouts'

    exclude '**/UrlMappings*'
    exclude '**/Application.class'
    exclude '**/Bootstrap.class'
    exclude '**/ErrorController.class'
    List excludes = ['BootStrap','Application','UrlMappings']
    eachFile { copyDetails ->
        if (copyDetails.path.contains('grails-plugin.xml')) {
            filter { line -> excludes.find{line.contains(it)}? null : line}
        }
    }
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'signing'

    group = "org.sitemesh"
    version "2.0-SNAPSHOT"

    java {
        sourceCompatibility = JavaVersion.toVersion("11")
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                from components.java
            }
        }
    }

    signing {
        sign publishing.publications.maven
    }
}



nexusPublishing {
    repositories {
        sonatype()
    }
}